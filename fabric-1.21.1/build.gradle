plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    // Use MC 1.21.1 mappings
    minecraft "net.minecraft:minecraft:1.21.1"
    mappings loom.officialMojangMappings()

    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    // Fabric API for 1.21.1
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version_1211"

    // Architectury API for 1.21.1
    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version_1211"

    // Version-agnostic common module
    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
    
    // Version-specific implementation (MC 1.21.1)
    common(project(path: ':common-1.21.1', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common-1.21.1', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
